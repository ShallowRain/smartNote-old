apply plugin: 'com.android.application'
apply plugin: 'img-optimizer'
//打包时，记得设置true启用
if (isNeedPackage.toBoolean()) {
    apply plugin: 'com.didiglobal.booster'
}

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "cn.rainss.smartNote"
        minSdkVersion 23
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }
    }

    signingConfigs {
        if (isNeedPackage.toBoolean()) {
            release {
                storeFile file(app_release.storeFile)
                storePassword app_release.storePassword
                keyAlias app_release.keyAlias
                keyPassword app_release.keyPassword
            }
        }

        debug {
            storeFile file("./debug.jks")
            storePassword "123456"
            keyAlias "debug"
            keyPassword "123456"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isNeedPackage.toBoolean()) {
                signingConfig signingConfigs.release

                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                def appID = properties.getProperty("APP_ID_UMENG")
                if (appID != null) {
                    buildConfigField "String", "APP_ID_UMENG", appID
                } else {
                    buildConfigField "String", "APP_ID_UMENG", '""'
                }
            } else {
                signingConfig signingConfigs.debug
                buildConfigField "String", "APP_ID_UMENG", '""'
            }
        }

        debug {
            debuggable true
            minifyEnabled false

            signingConfig signingConfigs.debug
            buildConfigField "String", "APP_ID_UMENG", '""'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation deps.junit
    androidTestImplementation deps.runner
    androidTestImplementation deps.espresso.core

    implementation project(':xrichtext')

    //分包
    implementation deps.androidx.multidex

    //下拉刷新
    implementation 'com.github.xuexiangjys.SmartRefreshLayout:refresh-header:1.1.5'
    implementation 'com.github.xuexiangjys.SmartRefreshLayout:refresh-layout:1.1.5'
    //WebView
    implementation 'com.github.xuexiangjys.AgentWeb:agentweb-core:1.0.0'
    implementation 'com.github.xuexiangjys.AgentWeb:agentweb-download:1.0.0'//选填
    //腾讯的键值对存储mmkv
    implementation 'com.tencent:mmkv:1.0.22'
    //屏幕适配AutoSize
    implementation 'me.jessyan:autosize:1.1.2'
    //umeng统计
    implementation 'com.umeng.umsdk:analytics:8.0.2'
    implementation 'com.umeng.umsdk:common:2.0.2'

    //ANR异常捕获
    implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'
    //美团多渠道打包
    implementation 'com.meituan.android.walle:library:1.1.6'

    //弹性布局
    implementation 'com.google.android:flexbox:0.3.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    //列表侧滑菜单
    implementation 'com.yanzhenjie.recyclerview:support:1.3.2'

    implementation 'com.github.iielse:ImageWatcher:1.1.5'
    //知乎图片选择器
    implementation 'com.zhihu.android:matisse:0.5.2-beta4'

    implementation 'com.github.bumptech.glide:okhttp3-integration:4.11.0'
    //日记
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation files('libs/systembartint-1.0.4.jar')

    //ECharts图表库
    implementation 'com.github.abel533:ECharts:3.0.0.2'

    //bmob
    implementation 'cn.bmob.android:bmob-sdk:3.7.8'
//    implementation "io.reactivex.rxjava2:rxjava:2.2.8"
//    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.okio:okio:2.2.2'
//    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'com.lxj:xpopup:1.5.2'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    configurations.all {
        exclude group: 'com.android.support', module: 'support-v13'
    }
}
//自动添加X-Library依赖
apply from: 'x-library.gradle'
//walle多渠道打包
apply from: 'multiple-channel.gradle'


